plugins {
  id 'com.android.application'
  id 'org.jetbrains.kotlin.android'
  id 'com.google.devtools.ksp'
  id 'com.diffplug.spotless'
  id 'androidx.navigation.safeargs.kotlin'
  id 'kotlin-parcelize'
  id 'kotlin-kapt'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
  keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
  compileSdk 34

  defaultConfig {
    applicationId 'com.iriswallet'
    minSdk 26
    targetSdk 34
    versionCode 63
    versionName '0.2.1'
    ndk {
      //noinspection ChromeOsAbiSupport
      abiFilters 'x86_64', 'arm64-v8a', 'armeabi-v7a'
    }
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  packagingOptions {
    jniLibs {
      useLegacyPackaging true
    }
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
    }
    release {
      storeFile file("${System.properties['user.home']}/android-keystores/iriswallet.jks")
      storePassword keystoreProperties['storePassword']
      keyAlias 'upload'
      keyPassword keystoreProperties['keyPassword']
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
      applicationIdSuffix '.debug'
      debuggable true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      shrinkResources false
      versionNameSuffix '-debug'
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      shrinkResources true
      signingConfig signingConfigs.release
    }
  }

  flavorDimensions += 'bitcoinNetwork'
  productFlavors {
    bitcoinSignet {
      applicationIdSuffix '.signet'
      manifestPlaceholders = [appName: '@string/app_name_signet']
      versionNameSuffix '-signet'
    }
    bitcoinTestnet {
      applicationIdSuffix '.testnet'
      manifestPlaceholders = [appName: '@string/app_name_testnet']
      versionNameSuffix '-testnet'
    }
    bitcoinMainnet {
      applicationIdSuffix '.mainnet'
      manifestPlaceholders = [appName: '@string/app_name_mainnet']
      versionNameSuffix '-mainnet'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_18
    targetCompatibility JavaVersion.VERSION_18
  }

  kotlinOptions {
    jvmTarget = '18'
  }

  buildFeatures {
    dataBinding true
    viewBinding true
  }

  lint {
    checkDependencies true
  }
  namespace 'com.iriswallet'
}

spotless {
  format 'misc', {
    target '**/*.gradle', '**/*.md', '**/.gitignore', '**/*.sh', '**/*.xml'
    trimTrailingWhitespace()
    indentWithSpaces()
    endWithNewline()
  }

  kotlin {
    target '**/*.kt'
    ktfmt().kotlinlangStyle()
  }
}

ksp {
  arg('room.schemaLocation', "$projectDir/schemas")
}

dependencies {
  // AndroidX
  implementation 'androidx.appcompat:appcompat:1.6.1'

  implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'

  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

  implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'

  implementation 'androidx.core:core-ktx:1.12.0'
  implementation 'androidx.core:core-splashscreen:1.0.1'

  implementation 'androidx.fragment:fragment-ktx:1.6.2'

  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
  implementation "androidx.lifecycle:lifecycle-service:$androidx_lifecycle_version"

  implementation "androidx.navigation:navigation-fragment-ktx:$androidx_navigation_version"
  implementation "androidx.navigation:navigation-ui-ktx:$androidx_navigation_version"

  implementation 'androidx.preference:preference-ktx:1.2.1'

  implementation "androidx.recyclerview:recyclerview:1.3.2"

  implementation "androidx.room:room-runtime:$room_version"
  annotationProcessor "androidx.room:room-compiler:$room_version"
  implementation "androidx.room:room-ktx:$room_version"
  ksp "androidx.room:room-compiler:$room_version"

  implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha06'

  implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

  implementation 'androidx.work:work-runtime-ktx:2.8.1'

  // Bitcoindevkit
  implementation 'org.bitcoindevkit:bdk-android:0.30.0'

  // Github
  implementation 'com.github.lelloman:android-identicons:v11'

  // Google
  implementation 'com.google.android.gms:play-services-auth:20.7.0'
  implementation "com.google.android.gms:play-services-drive:17.0.0"

  implementation 'com.google.android.material:material:1.10.0'

  implementation('com.google.api-client:google-api-client-android:2.2.0') {
    exclude group: 'org.apache.httpcomponents'
    exclude module: 'guava-jdk5'
  }

  implementation('com.google.apis:google-api-services-drive:v3-rev20230822-2.0.0') {
    exclude group: 'org.apache.httpcomponents'
    exclude module: 'guava-jdk5'
  }

  implementation 'com.google.code.gson:gson:2.10.1'

  // Journeyapps
  implementation 'com.journeyapps:zxing-android-embedded:4.3.0'

  // RGB-Tools
  implementation 'org.rgbtools:rgb-lib-android:0.2.0'

  // Squareup
  implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'

  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
}
